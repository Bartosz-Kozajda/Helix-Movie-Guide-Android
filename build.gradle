def apiProperties = rootProject.file('secure/api.properties')

ext {
    config = [
            baseUrl: "http://api.themoviedb.org/3/",
            apiKey : getProperty('apiKey', apiProperties)
    ]
}

buildscript {

    ext {
        versions = [
                kotlin: '1.1.3',
                rxjava: '2.0.7',
                gson  : '2.8.0'
        ]

        androidVersions = [
                rxAndroid         : '2.0.1',
                support           : '25.3.0',
                constraint        : '1.0.2',
                retrofit          : '2.2.0',
                loggingInterceptor: '3.6.0'
        ]

        testVersions = [
                junit: '4.12',
                mockito: '2.8.47'
        ]
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$project.versions.kotlin"
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

static def getProperty(String prop, File propsFile) {
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        return props[prop]
    } else {
        return System.getenv(prop)
    }
}