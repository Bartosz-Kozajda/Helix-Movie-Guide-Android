def apiProperties = rootProject.file('secure/api.properties')

ext {
    config = [
            baseUrl       : "http://api.themoviedb.org/3/",
            apiKey        : getProperty('apiKey', apiProperties),
            poeditorApiKey: getProperty('poeditorApiKey', apiProperties)
    ]
}

buildscript {

    ext {
        versions = [
                kotlin  : '1.1.51',
                kategory: '0.3.10',
                inject  : '1',
                dagger  : '2.11',
                rxjava  : '2.1.5',
                gson    : '2.8.0'
        ]

        androidVersions = [
                timber            : '4.6.0',
                rxAndroid         : '2.0.1',
                arch              : '1.0.0',
                support           : '26.1.0',
                constraint        : '1.0.2',
                retrofit          : '2.2.0',
                loggingInterceptor: '3.6.0',
                picasso           : '2.5.2',
                rxbinding         : '2.0.0'
        ]

        quality = [
                jacoco: '0.7.9',
                ktlint: '0.11.1'
        ]

        testVersions = [
                junit        : '4.12',
                mockito      : '2.8.47',
                mockitoKotlin: '1.5.0',
                kluent       : '1.30'
        ]
    }

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0'
        classpath 'be.lukin.poeditor:gradle:0.3.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$project.versions.kotlin"

    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

static def getProperty(String prop, File propsFile) {
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        return props[prop]
    } else {
        return System.getenv(prop)
    }
}