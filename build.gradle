def apiProperties = rootProject.file('secure/api.properties')

ext {
    config = [
            baseUrl       : "http://api.themoviedb.org/3/",
            apiKey        : getProperty('apiKey', apiProperties),
            poeditorApiKey: getProperty('poeditorApiKey', apiProperties)
    ]
}

buildscript {

    ext {
        pluginsVersions = [
                gradle        : '3.1.3',
                poeditor      : '0.3.3',
                gradleVersions: '0.17.0',
                ktlint        : '3.2.0',
                detekt        : '1.0.0.RC6-4'
        ]

        versions = [
                kotlin: '1.2.50',
                rxjava: '2.1.14',
                gson  : '2.8.5'
        ]

        androidVersions = [
                koin              : '0.9.3',
                timber            : '4.7.0',
                rxAndroid         : '2.0.2',
                arch              : '1.1.1',
                support           : '27.1.1',
                constraint        : '1.1.0',
                retrofit          : '2.4.0',
                loggingInterceptor: '3.10.0',
                glide             : '4.7.1',
                rxbinding         : '2.1.1'
        ]

        testVersions = [
                junit        : '4.12',
                mockito      : '2.16.0',
                mockitoKotlin: '1.5.0',
                kluent       : '1.35'
        ]
    }

    repositories {
        jcenter()
        google()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$project.pluginsVersions.gradle"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$project.versions.kotlin"

        classpath "be.lukin.poeditor:gradle:$project.pluginsVersions.poeditor"
        classpath "com.github.ben-manes:gradle-versions-plugin:$project.pluginsVersions.gradleVersions"
        classpath "gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:$project.pluginsVersions.ktlint"
        classpath "gradle.plugin.io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$project.pluginsVersions.detekt"
    }
}

apply plugin: "io.gitlab.arturbosch.detekt"

allprojects {
    repositories {
        jcenter()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

static def getProperty(String prop, File propsFile) {
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        return props[prop]
    } else {
        return System.getenv(prop)
    }
}